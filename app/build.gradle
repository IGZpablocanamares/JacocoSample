apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco'

jacoco {
	toolVersion = "0.8.1"
	reportsDir = file("$buildDir/../../report")
}

tasks.withType(Test) {
	jacoco.includeNoLocationClasses = true
}

task customTestReport(type: JacocoReport, dependsOn: [':core:test', 'testDebugUnitTest', 'createDebugCoverageReport']) {
	group = "Reporting"
	description = "Generate Jacoco coverage reports for Debug build"
	
	reports {
		html.enabled = true
	}
	
	// what to exclude from coverage report
	// UI, "noise", generated classes, platform classes, etc.
	def excludes = [
		'**/R.class',
		'**/R$*.class',
		'**/*$ViewInjector*.*',
		'**/BuildConfig.*',
		'**/Manifest*.*',
		'**/*Test*.*',
		'android/**/*.*'
	]
	// generated classes
	classDirectories = fileTree(
		dir: "$buildDir/intermediates/classes/debug",
		excludes: excludes
	) + fileTree(
		dir: "$buildDir/tmp/kotlin-classes/debug",
		excludes: excludes
	) + fileTree(
		dir: "$buildDir/../../core/build/classes",
		excludes: excludes
	)

	// sources
	sourceDirectories = files([
		android.sourceSets.main.java.srcDirs,
		"src/main/kotlin",
		"../core/src/main"
	])
	executionData = fileTree(dir: project.buildDir, includes: [
		'jacoco/testDebugUnitTest.exec',
		'outputs/code-coverage/connected/*coverage.ec',
		'../../core/build/jacoco/*.ec',
	])
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.intelygenz.jacocosample"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
	
    buildTypes {
		debug {
			testCoverageEnabled true
		}
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
	
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation project(":core")
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    testImplementation 'junit:junit:4.12'
	androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
