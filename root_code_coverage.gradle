apply plugin: 'jacoco'

jacoco {
	toolVersion = "0.8.1"
}

tasks.withType(Test) {
	jacoco.includeNoLocationClasses = true
}

task rootCodeCoverage(type: JacocoReport, dependsOn: [':app:codeCoverage',
												  ':core:codeCoverage',
												  ':coreDeps:codeCoverage',
												  ':coreSupport:codeCoverage']) {
	group = "Reporting"
	description = "Generate Jacoco coverage reports for Debug build"
	
	reports {
		xml.enabled false
		csv.enabled false
		html.enabled = true
		html.destination file("${rootProject.rootDir}/code-coverage")
	}
	
	// what to exclude from coverage report
	// UI, "noise", generated classes, platform classes, etc.
	def excludes = [
		'**/R.class',
		'**/R$*.class',
		'**/*$ViewInjector*.*',
		'**/BuildConfig.*',
		'**/Manifest*.*',
		'**/*Test*.*',
		'android/**/*.*'
	]
	// generated classes
	classDirectories = fileTree(
		dir: "${rootProject.rootDir}/app/build/intermediates/classes/debug",
		excludes: excludes
	) + fileTree(
		dir: "${rootProject.rootDir}/app/build/tmp/kotlin-classes/debug",
		excludes: excludes
	) + fileTree(
		dir: "${rootProject.rootDir}/core/build/classes",
		excludes: excludes
	) + fileTree(
		dir: "${rootProject.rootDir}/coreDeps/build/classes",
		excludes: excludes
	) + fileTree(
		dir: "${rootProject.rootDir}/coreSupport/build/intermediates/classes/debug",
		excludes: excludes
	) + fileTree(
		dir: "${rootProject.rootDir}/coreSupport/build/tmp/kotlin-classes/debug",
		excludes: excludes
	)
	
	// sources
	def coverageSourceDirs = [
		"$rootProject.rootDir/app/src/main",
		"$rootProject.rootDir/core/src/main",
		"$rootProject.rootDir/coreDeps/src/main",
		"$rootProject.rootDir/coreSupport/src/main"
	]
	additionalSourceDirs = files(coverageSourceDirs)
	sourceDirectories = files(coverageSourceDirs)
	executionData = fileTree(dir: rootProject.rootDir, includes: ['**/*.exec', '**/*.ec'])
	
}